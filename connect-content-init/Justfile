set positional-arguments

BUILDX_PATH := ""

IMAGE_PREFIX := "rstudio-"
PRODUCT := "connect-content-init"
IMAGE_OS := "ubuntu1804"

RSC_VERSION := "2023.05.0"
RSC_TAG_SAFE_VERSION := replace(RSC_VERSION, "+", "-")
RSC_LICENSE := ""

R_VERSION := "3.6.2"
R_VERSION_ALT := "4.1.0"

PYTHON_VERSION := "3.9.5"
PYTHON_VERSION_ALT := "3.8.10"

DEFAULT_TAG := IMAGE_PREFIX + PRODUCT + ":" + IMAGE_OS + "-" + RSC_TAG_SAFE_VERSION

# Build Connect content init image - just build ubuntu1804 2022.10.0 rstudio/rstudio-connect-content-init:ubuntu1804-2022.10.0
build OS=IMAGE_OS VERSION=RSC_VERSION +TAGS=DEFAULT_TAG:
  #!/usr/bin/env bash
  set -euxo pipefail
  BUILDX_ARGS=""
  if [[ "{{BUILDX_PATH}}" != "" ]]; then
    BUILDX_ARGS="--cache-from=type=local,src=/tmp/.buildx-cache --cache-to=type=local,dest=/tmp/.buildx-cache"
  fi
  tag_array=()
  for TAG in {{TAGS}}
  do
    tag_array+=("-t" $TAG)
  done

  docker buildx --builder="{{ BUILDX_PATH }}" build --load ${BUILDX_ARGS} \
    ${tag_array[@]} \
    --build-arg RSC_VERSION="{{ VERSION }}" \
    --file=./Dockerfile.$(just -f ../Justfile _parse-os {{OS}}) .

# Test Connect content init image - just test rstudio/rstudio-connect:ubuntu1804-2022.10.0 2022.10.0
test TAG=DEFAULT_TAG VERSION=RSC_VERSION CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  IMAGE_NAME="{{ TAG }}" \
  RSC_VERSION="{{ VERSION }}" \
  docker-compose -f ./docker-compose.test.yml run sut {{ CMD }}

# Test Connect content init image interactively - just test-i rstudio/rstudio-connect:ubuntu1804-2022.10.0 2022.10.0
test-i TAG=DEFAULT_TAG VERSION=RSC_VERSION:
  just test {{ TAG }} {{ VERSION }} bash
