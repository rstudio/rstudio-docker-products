set positional-arguments

BUILDX_PATH := ""

IMAGE_PREFIX := "rstudio-"
PRODUCT := "connect-content-init"
IMAGE_OS := "bionic"

RSC_VERSION := "2022.09.0"
RSC_get-tag-safe-version := replace(RSC_VERSION, "+", "-")
RSC_LICENSE := ""

DEFAULT_TAG := IMAGE_PREFIX + PRODUCT + ":" + IMAGE_OS + "-" + RSC_get-tag-safe-version

# Build Connect content init image - just build bionic 2022.09.0 rstudio/rstudio-connect-content-init:bionic-2022.09.0
build OS=IMAGE_OS VERSION=RSC_VERSION +TAGS=DEFAULT_TAG:
  #!/usr/bin/env bash
  set -euxo pipefail
  BUILDX_ARGS=""
  if [[ "{{BUILDX_PATH}}" != "" ]]; then
    BUILDX_ARGS="--cache-from=type=local,src=/tmp/.buildx-cache --cache-to=type=local,dest=/tmp/.buildx-cache"
  fi
  tag_array=()
  for TAG in {{TAGS}}
  do
    tag_array+=("-t" $TAG)
  done

  docker buildx --builder="{{ BUILDX_PATH }}" build --load ${BUILDX_ARGS} \
    ${tag_array[@]} \
    --build-arg RSC_VERSION="{{ VERSION }}" \
    --file=./Dockerfile."{{ OS }}" .

# Test Connect content init image - just test rstudio/rstudio-connect:bionic-2022.09.0 2022.09.0
test TAG=DEFAULT_TAG VERSION=RSC_VERSION CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  IMAGE_NAME="{{ TAG }}" \
  RSC_VERSION="{{ VERSION }}" \
  docker-compose -f ./docker-compose.test.yml run sut {{ CMD }}

# Test Connect content init image interactively - just test-i rstudio/rstudio-connect:bionic-2022.09.0 2022.09.0
test-i TAG=DEFAULT_TAG VERSION=RSC_VERSION:
  just test {{ TAG }} {{ VERSION }} bash
