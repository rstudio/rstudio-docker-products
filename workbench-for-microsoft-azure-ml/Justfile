set positional-arguments

BUILDX_PATH := ""

IMAGE_PREFIX := "rstudio-"
PRODUCT := "workbench"
IMAGE_OS := "bionic"

RSW_VERSION := "2022.07.2+576.pro12"
RSW_get-tag-safe-version := replace(RSW_VERSION, "+", "-")
RSW_LICENSE := ""

R_VERSION := "3.6.2"
R_VERSION_ALT := "4.1.0"

PYTHON_VERSION := "3.9.5"
PYTHON_VERSION_ALT := "3.8.10"

DEFAULT_TAG := IMAGE_PREFIX + PRODUCT + ":" + IMAGE_OS + "-" + RSW_get-tag-safe-version

# Build Workbench for Azure ML image - just build bionic 2022.07.2+576.pro12 rstudio/rstudio-workbench:bionic-2022.07.2-576.pro12
build OS=IMAGE_OS VERSION=RSW_VERSION +TAGS=DEFAULT_TAG:
  #!/usr/bin/env bash
  set -euxo pipefail
  BUILDX_ARGS=""
  if [[ "{{BUILDX_PATH}}" != "" ]]; then
    BUILDX_ARGS="--cache-from=type=local,src=/tmp/.buildx-cache --cache-to=type=local,dest=/tmp/.buildx-cache"
  fi
  tag_array=()
  for TAG in {{TAGS}}
  do
    tag_array+=("-t" $TAG)
  done

  docker buildx --builder="{{ BUILDX_PATH }}" build --load ${BUILDX_ARGS} \
    ${tag_array[@]} \
    --build-arg RSW_VERSION="{{ VERSION }}" \
    --build-arg R_VERSION="{{ R_VERSION }}" \
    --build-arg R_VERSION_ALT="{{ R_VERSION_ALT }}" \
    --build-arg PYTHON_VERSION="{{ PYTHON_VERSION }}" \
    --build-arg PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
    --file=./Dockerfile."{{ OS }}" .

# Test Workbench for Azure ML image - just test rstudio/rstudio-workbench:bionic-2022.07.2-576.pro12 2022.07.2+576.pro12
test TAG=DEFAULT_TAG VERSION=RSW_VERSION CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  IMAGE_NAME="{{ TAG }}" \
  RSW_VERSION="{{ VERSION }}" \
  R_VERSION="{{ R_VERSION }}" \
  R_VERSION_ALT="{{ R_VERSION_ALT }}" \
  PYTHON_VERSION="{{ PYTHON_VERSION }}" \
  PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
  docker-compose -f ./docker-compose.test.yml run sut {{ CMD }}

# Test Workbench for Azure ML image interactively - just test-i rstudio/rstudio-workbench:bionic-2022.07.2-576.pro12 2022.07.2+576.pro12
test-i TAG=DEFAULT_TAG VERSION=RSW_VERSION:
  just test {{ TAG }} {{ VERSION }} bash

# Run Workbench for Azure ML - just RSW_LICENSE="<RSW license>" run rstudio/r-session-complete:bionic-2022.07.2-576.pro12
run TAG=DEFAULT_TAG CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  if [ -z "{{ RSW_LICENSE }}" ]; then
    echo "Please set RSW_LICENSE to a valid RStudio Workbench license before running."
    exit 1
  fi
  docker run -it --privileged \
    -p 8787:8787 \
    -e RSW_LICENSE="{{ RSW_LICENSE }}" \
    "{{ TAG }}" {{ CMD }}
