set positional-arguments

BUILDX_PATH := ""

IMAGE_PREFIX := ""
PRODUCT := "r-session-complete"
IMAGE_OS := "ubuntu1804"

RSW_VERSION := "2022.12.0+353.pro20"
RSW_LICENSE := ""

DRIVERS_VERSION := "2022.11.0"
DRIVERS_VERSION_RHEL := DRIVERS_VERSION + "-1"

R_VERSION := "4.1.0"
R_VERSION_ALT := "3.6.2"

PYTHON_VERSION := "3.9.5"
PYTHON_VERSION_ALT := "3.8.10"

_make-default-tag OS=IMAGE_OS:
  echo "{{IMAGE_PREFIX}}{{PRODUCT}}:{{OS}}-$(just -f ../Justfile _get-tag-safe-version {{RSW_VERSION}})"

# Build r-session-complete image - just build ubuntu1804 2022.07.2+576.pro12 rstudio/r-session-complete:ubuntu1804-2022.07.2-576.pro12
build OS=IMAGE_OS VERSION=RSW_VERSION *TAGS="":
  #!/usr/bin/env bash
  set -euxo pipefail
  BUILDX_ARGS=""
  if [[ "{{BUILDX_PATH}}" != "" ]]; then
    BUILDX_ARGS="--cache-from=type=local,src=/tmp/.buildx-cache --cache-to=type=local,dest=/tmp/.buildx-cache"
  fi
  if [[ "{{ OS }}" == "centos7" ]]; then
    _DRIVERS_VERSION="{{ DRIVERS_VERSION_RHEL }}"
  else
    _DRIVERS_VERSION="{{ DRIVERS_VERSION }}"
  fi

  if [[ "{{TAGS}}" == "" ]]; then
    raw_tag_array=($(just _make-default-tag {{OS}}))
  else
    raw_tag_array=("{{TAGS}}")
  fi

  tag_array=()
  for tag in $raw_tag_array
  do
    tag_array+=("-t" $tag)
  done

  docker buildx --builder="{{ BUILDX_PATH }}" build --load ${BUILDX_ARGS} \
    ${tag_array[@]} \
    --build-arg RSW_VERSION="{{ VERSION }}" \
    --build-arg DRIVERS_VERSION="${_DRIVERS_VERSION}" \
    --build-arg R_VERSION="{{ R_VERSION }}" \
    --build-arg R_VERSION_ALT="{{ R_VERSION_ALT }}" \
    --build-arg PYTHON_VERSION="{{ PYTHON_VERSION }}" \
    --build-arg PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
    --file=./Dockerfile.$(just -f ../Justfile _parse-os {{OS}}) .

# Test r-session-complete image - just test rstudio/r-session-complete:ubuntu1804-2022.07.2-576.pro12 2022.07.2+576.pro12
test TAG=`just _make-default-tag` VERSION=RSW_VERSION CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  IMAGE_NAME="{{ TAG }}" \
  RSW_VERSION="{{ VERSION }}" \
  R_VERSION="{{ R_VERSION }}" \
  R_VERSION_ALT="{{ R_VERSION_ALT }}" \
  PYTHON_VERSION="{{ PYTHON_VERSION }}" \
  PYTHON_VERSION_ALT="{{ PYTHON_VERSION_ALT }}" \
  docker-compose -f ./docker-compose.test.yml run sut {{ CMD }}

# Test r-session-complete image interactively - just test-i rstudio/r-session-complete:ubuntu1804-2022.07.2-576.pro12 2022.07.2+576.pro12
test-i TAG=`just _make-default-tag` VERSION=RSW_VERSION:
  just test {{ TAG }} {{ VERSION }} bash

# Run r-session-complete - just run rstudio/r-session-complete:ubuntu1804-2022.07.2-576.pro12
run TAG=`just _make-default-tag` CMD="":
  #!/usr/bin/env bash
  set -euxo pipefail
  if [ -z "{{ RSW_LICENSE }}" ]; then
    echo "Please set RSW_LICENSE to a valid RStudio Workbench license before running."
    exit 1
  fi
  docker run -it --privileged \
    -p 8788:8788 \
    -e RSW_LICENSE="{{ RSW_LICENSE }}" \
    "{{ TAG }}" {{ CMD }}
